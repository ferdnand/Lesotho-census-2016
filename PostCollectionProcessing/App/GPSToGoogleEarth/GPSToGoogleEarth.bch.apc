{
Exports GPS from enumerator data files to a KML file
that can be viewed in Google Earth.

Each household is displayed using the standard placemark
icon (the pushpin). The icons are colored according to
household status.

Households are grouped by EA, Constituency and District
as nested folders in the KML file.

For each household we display:
	- Household number, head name
	- Village number/name
	- EA code
	- Household status
	- GPS accuracy (as reported by tablet)
	
To make it run efficiently in Google Earth, bounding regions
are computed for each EA. This allows Google Earth to only
try to draw the points for the EAs that are currently in view.
In addition, the bounding region is also set with a level of
detail so that the points in the EA are only drawn if you
are zoomed close enough so that the EA takes up more than 128
pixels on the screen. The bounding region for the EA is just
a rectangle that encloses all the GPS readings found in the EA.

}
PROC GLOBAL
numeric lastEA = -1;
numeric lastDistrict = -1;
numeric lastConstituency = -1;
array eaBounds(4); // min lat, max lat, min long, max long

// To color placemarks differently need to add style and style map for each color.
function writePlacemarkStyle(string id, scale, string color, highlightScale, string highlightColor)
	write('<Style id="%s_norm"><IconStyle><color>%s</color><scale>%f</scale></IconStyle><LabelStyle><scale>0</scale></LabelStyle></Style>',
		id, color, scale);
	write('<Style id="%s_hi"><IconStyle><color>%s</color><scale>%f</scale></IconStyle></Style>',
		id, highlightColor, highlightScale);
	write('<StyleMap id="%s">', id);
	write('<Pair><key>normal</key><styleUrl>#%s_norm</styleUrl></Pair>',
		id);
	write('<Pair><key>highlight</key><styleUrl>#%s_hi</styleUrl></Pair>',
		id);
	write('</StyleMap>');
end;

function writePoint()

	string style;
	if HOUSEHOLD_STATUS in 2:5, 7 then
		style = maketext("#ps_%d", HOUSEHOLD_STATUS);
	else
		style = maketext("#ps_na");
	endif;	

	string name = maketext("<![CDATA[%d - %s]]>", HOUSEHOLD_NUMBER, strip(HOUSEHOLD_HEAD_NAMES));		
	string description = "<![CDATA[<table>" +
		  maketext("<tr><td>EA</th><td>%011d</td></tr>", ENUMERATION_AREA) +
		  maketext("<tr><td>Village</th><td>%d - %s</td></tr>", VILLAGE_NUMBER, strip(VILLAGE_NAME)) +
		  maketext("<tr><td>Status</th><td>%s</td></tr>", getlabel(HOUSEHOLD_STATUS, HOUSEHOLD_STATUS)) +
		  maketext("<tr><td>Accuracy</th><td>%f</td></tr>", HH_ACCURACY) +
		  "</table>]]>";
 	write("<Placemark><name>%s</name><styleUrl>%s</styleUrl><description>%s</description><Point><coordinates>%f,%f</coordinates></Point></Placemark>",
			name, style, description, HH_LONGITUDE, HH_LATITUDE);
end;

// Empty out the EA bounds region.
function clearEaBounds()
	// Latitude runs from -90 to 90 and longitude
	// from -180 to 180 so we set bounds to values
	// outside that range to represent an empty
	// box.
	eaBounds(1) = -91;
	eaBounds(2) = 91;
	eaBounds(3) = -181;
	eaBounds(4) = 181;
end;

// Expand the EA bounding region to include
// the current lat/lon point.
function updateEaBounds()
	if HH_LATITUDE > eaBounds(1) then
		eaBounds(1) = HH_LATITUDE;
	endif;
	if HH_LATITUDE < eaBounds(2) then
		eaBounds(2) = HH_LATITUDE;
	endif;
	if HH_LONGITUDE > eaBounds(3) then
		eaBounds(3) = HH_LONGITUDE;
	endif;
	if HH_LONGITUDE < eaBounds(4) then
		eaBounds(4) = HH_LONGITUDE;
	endif;
end;

// Write bounding region for current EA
function writeEaBounds()
	if eaBounds(1) >= -90 and eaBounds(2) <= 90 and eaBounds(3) >= -180 and eaBounds(4) <= 180 then
		write('<Region><LatLonAltBox><north>%f</north><south>%f</south><east>%f</east><west>%f</west></LatLonAltBox><Lod><minLodPixels>128</minLodPixels></Lod></Region>',
			eaBounds(1), eaBounds(2), eaBounds(3), eaBounds(4));
	endif;
end;

PROC HOUSEHOLDDATAENTRY_FF
preproc

clearEaBounds();

write('<?xml version="1.0" encoding="UTF-8"?>');
write('<kml xmlns="http://www.opengis.net/kml/2.2">');
write('<Document>');
write('<name>Census 2016 - Households</name>');

// Set up styles for each houshold status.
// Make the scale be 0.5 so that placemark icons
// are half the usual size otherwise it is too hard see
// anything besides the icons. Use the default size when highlighted.
writePlacemarkStyle("ps_2", 0.5, 'ff000055', 1.0, 'ff000055');
writePlacemarkStyle("ps_3", 0.5, 'ff00aaff', 1.0, 'ff00aaff');
writePlacemarkStyle("ps_4", 0.5, 'ff00ffff', 1.0, 'ff00ffff');
writePlacemarkStyle("ps_5", 0.5, 'ff0000ff', 1.0, 'ff0000ff');
writePlacemarkStyle("ps_7", 0.5, 'ff00aa00', 1.0, 'ff00aa00');
writePlacemarkStyle("ps_na", 0.5, 'ff4a4a4a', 1.0, 'ff4a4a4a');

// Set initial view to look at point roughly in the middle of Lesotho
write('<LookAt><longitude>28.253637</longitude><latitude>-29.710970</latitude><altitude>0</altitude><range>400000</range></LookAt>');

postproc
if lastEa <> -1 then
	writeEaBounds();
	write('</Folder>');
endif;
if lastConstituency <> -1 then
	write('</Folder>');
endif;
if lastDistrict <> -1 then
	write('</Folder>');
endif;
write('</Document>');
write('</kml>');

PROC LESOTHOCENSUS2016_QUEST
if HH_LONGITUDE <> notappl and HH_LATITUDE <> notappl then
		
	if ENUMERATION_AREA <> lastEA and lastEA <> -1 then
		writeEaBounds();
		clearEaBounds();
		write('</Folder>');	
	endif;
	
	if CONSTITUENCY <> lastConstituency and lastConstituency <> -1 then
		write('</Folder>');
	endif;

	if DISTRICT <> lastDistrict and lastDistrict <> -1 then
		write('</Folder>');
	endif;
	
	if DISTRICT <> lastDistrict then
		string districtName = getlabel(DISTRICT, DISTRICT);
		if districtName = "" then
			errmsg("Missing district name");
			districtName = maketext("District - %d", DISTRICT);
		endif;
		write('<Folder><name>%s</name>', districtName);
		lastDistrict = DISTRICT;
	endif;
	
	if CONSTITUENCY <> lastConstituency then
		string constituencyName = getlabel(DISTRICT, DISTRICT);
		if constituencyName = "" then
			errmsg("Missing constituency name");
			constituencyName = maketext("Constituency - %d", CONSTITUENCY);
		endif;
		write('<Folder><name>%s</name>', getlabel(CONSTITUENCY, CONSTITUENCY));
		lastConstituency = CONSTITUENCY;
	endif;

	if ENUMERATION_AREA <> lastEA then
		write('<Folder><name>%011d</name>', ENUMERATION_AREA);
		lastEA = ENUMERATION_AREA;	
	endif;

	writePoint();
	updateEaBounds();
endif;
